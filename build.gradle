
ext {
    dataDirectory = 'data'
}

task generate << {
    // 対象ファイル作成
    file('index.html').with { file ->
        file.createNewFile()
        file.setText(buildHtml(readBookmarkData()))
    }
}

def readBookmarkData() {
    def slurper = new groovy.json.JsonSlurper()
    file(dataDirectory)\
        .listFiles({it.isFile()} as FileFilter)\
        .inject([]) { bookmarks, file ->
            bookmarks += slurper.parseText(file.text).bookmarks?:[]
        }
}

def buildHtml(def bookmarks) {
    def writer = new StringWriter()
    def builder = new groovy.xml.MarkupBuilder(writer)
    builder.html {
        head {
            title "bookmarks"
        }
        body {
            ul {
                bookmarks.each { bookmark ->
                    li {
                        a (href:bookmark.url, bookmark.title)
                    }
                }
            }
        }
    }
    writer.toString()
}

task printDataFiles << {
    println "data directory is ${dataDirectory}"
    file(dataDirectory).eachFile(FileType.FILES) { File file ->
        println "# " + file.name
        println file.getText()
    }
}

